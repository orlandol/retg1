
program <-
  _ programDeclaration
    (_ globalDeclaration)*
  _ runBlock
    (_ globalDeclaration)*
  _ eof

#
# Qualified identifier grammar parses each component in the correct order
#
#qident <-
#  qident _ '.' _ <IDENT> {
#    printf( "QIDENT: '%s'\n", $1 );
#  }
#/ <IDENT> {
#    printf( "QIDENT: '%s'\n", $2 );
#  }

runBlock <-
  'run'
    (_ !'run' localVarBlock)*
    (_ ('return' _ EXPR / STATEMENT))*
  _ 'end'

programDeclaration <- 'program' _ <IDENT> {
  printf( "[L%u] PROGRAM: '%s'\n", auxil->line, $1 );
}

TYPESPEC <-
  ('@' / '#') _ <TYPENAME> _ '[' _ ']' {
  }
/ ('@' / '#') _ <TYPENAME> _ '[' _ CONSTINDEXEXPR _ ']' {
  }
/ ('@' / '#') _ <TYPENAME> {
  }
/ <TYPENAME> _ '[' _ CONSTINDEXEXPR _ ']' {
  }
/ <TYPENAME>

TYPENAME <- (BASICTYPE / IDENT)

CONSTINDEXEXPR <-
  CONSTINDEXINT

CONSTINDEXINT <-
  '0b'<[_0-1]+> {
    printf( "  [BININT: %s]\n", $1 );
  }
/ '0o'<[_0-7]+> {
    printf( "  [OCTINT: %s]\n", $2 );
  }
/ '0x'<[_0-9a-fA-F]+> {
    printf( "  [HEXINT: %s]\n", $3 );
  }
/ <[_0-9]+> {
    printf( "  [INTEGER: %s]\n", $4 );
  }

globalDeclaration <-
  constDeclaration
/ enumBlock
/ typeAliasDeclaration
/ structBlock
/ funcImport
/ funcDeclaration
/ funcBlock

constDeclaration <-
  'const' _ BASICTYPE _ <IDENT> _ '=' _ CONSTINIT {
    printf( "CONST: '%s'\n", $1 );
  }

enumBlock <-
  enumDeclaration
  (_ !'end' enumFieldDeclaration)*
  _ 'end'

enumDeclaration <-
  'enum' _ BASICTYPE _ <IDENT> {
    printf( "ENUM: '%s'\n", $1 );
  }

enumFieldDeclaration <-
  enumFieldDeclaration _ ',' _ enumFieldDeclaration
/ enumSingleFieldAssignment
/ enumSingleFieldDeclaration

enumSingleFieldAssignment <-
  <IDENT> _ '=' _ CONSTEXPR {
    printf( "ENUMFIELD: '%s'\n", $1 );
  }

enumSingleFieldDeclaration <-
  <IDENT> {
    printf( "ENUMFIELD: '%s'\n", $1 );
  }

typeAliasDeclaration <-
  'type' _ (!reservedWord TYPESPEC) _ <IDENT> {
    printf( "TYPE: '%s'\n", $1 );
  }
/ 'type' _ <IDENT> {
    printf( "TYPE: '%s'\n", $2 );
  }

structBlock <-
  structDeclaration
  (_ !'end' structInherit)*
  (_ !'end' structFieldDeclaration)*
  _ 'end' {
  }

structDeclaration <-
  'struct' _ 'packed'? _ <IDENT> {
    printf( "STRUCT: '%s'\n", $1 );
  }

structInherit <-
  'inherits' _ structInheritName (_ ',' _ structInheritName)*

structInheritName <-
  <IDENT> {
    printf( "  STRUCTINHERIT: '%s'\n", $1 );
  }

structFieldDeclaration <-
  structNestedStructBlockField
/ structNestedUnionFieldDeclaration
/ TYPESPEC _ <IDENT> {
    printf( "  STRUCTFIELD: '%s'\n", $1 );
  }
/ ',' _ <IDENT> {
    printf( "  STRUCTFIELD: '%s'\n", $2 );
  }
/ structFieldDeclaration

structNestedStructBlockField <-
  structNestedStructDeclarationField
  (_ !'end' structNestedStructNestedFieldDeclaration)*
  _ 'end'

structNestedStructDeclarationField <-
  'struct' _ <IDENT> {
    printf( "  STRUCTNESTEDSTRUCTFIELD: '%s'\n", $1 );
  }

structNestedStructNestedFieldDeclaration <-
  TYPESPEC _ <IDENT> {
    printf( "    STRUCTNESTEDSTRUCTNESTEDFIELD: '%s'\n", $1 );
  }
/ ',' _ <IDENT> {
    printf( "    STRUCTNESTEDSTRUCTNESTEDFIELD: '%s'\n", $2 );
  }
/ structNestedStructNestedFieldDeclaration

structNestedUnionFieldDeclaration <-
  structNestedUnionFieldDeclareName _ '(' (_ structNestedUnionType)+ _ ')'

structNestedUnionFieldDeclareName <-
  'union' _ <IDENT> {
    printf( "  STRUCTNESTEDUNIONFIELD: '%s'\n", $1 );
  }

structNestedUnionType <-
  <TYPESPEC> {
    printf( "    STRUCTNESTEDUNIONFIELDTYPE: '%s'\n", $1 );
  }
/ ',' _ <TYPESPEC> {
    printf( "    STRUCTNESTEDUNIONFIELDTYPE: '%s'\n", $2 );
  }

funcImport <-
  'func' _ 'import' _ CALLSPEC _ IDENT _ '(' (_ funcParamDeclaration)* _ ')'
  _ funcImportFrom _ funcImportLinkAs {
  }
/ 'func' _ 'import' _ CALLSPEC _ TYPESPEC _ IDENT _ '(' (_ funcParamDeclaration)* _ ')'
  _ funcImportFrom? _ funcImportLinkAs? {
  }

funcImportFrom <- ('from' _ <FILENAME>)

funcImportLinkAs <- ('as' _ <LINKNAME>)

FILENAME <- STRING

LINKNAME <- STRING

funcDeclaration <-
  'func' _ 'declare' _ CALLSPEC _ IDENT _ '(' (_ funcParamDeclaration)* _ ')'
/ 'func' _ 'declare' _ TYPESPEC _ CALLSPEC _ IDENT _ '(' (_ funcParamDeclaration)* _ ')'

funcBlock <-
  'func' _ CALLSPEC _ FUNCSPEC _ IDENT _ '(' (_ funcParamDeclaration)* _ ')'
    (_ localVarBlock)*
    (_ ('return' / STATEMENT))*
  _ 'end'
/ 'func' _ TYPESPEC _ CALLSPEC _ FUNCSPEC _ IDENT _ '(' (_ funcParamDeclaration)* _ ')'
    (_ localVarBlock)*
    (_ ('return' _ EXPR / STATEMENT))*
  _ 'end'

funcParamDeclaration <-
  TYPESPEC _ (!reservedWord IDENT) (_ ',' _ funcParamDeclaration)* {
  }

STRING <- DOUBLEQUOTESTRING / SINGLEQUOTESTRING

DOUBLEQUOTESTRING <- '"' (!'"' .)* '"'

SINGLEQUOTESTRING <- "'" (!"'" .)* "'"

FUNCSPEC <- ('asm')? _ ('noframe')?

CALLSPEC <- ('cdecl' / 'stdcall')?

localVarBlock <-
  'var'
  (_ !'end' localVarDeclaration)*
  _ 'end'

localVarDeclaration <-
  TYPESPEC _ VARIDENT _ (',' _ VARIDENT _)*

VARIDENT <-
  reservedWord {
    printf( "Reserved word declared on line %u\n", auxil->line );
    exit( 1 );
  }
/ <IDENT> {
    printf( "VARIDENT: '%s'\n", $1 );
  }

reservedWord <-
  'program' / 'uses' / 'const' / 'enum' / 'newtype' / 'type' / 'var'
  / 'func' / 'asm' / 'noframe' / 'object' / 'interface' / 'inherits'
  / 'implements' / 'constructor' / 'destructor' / 'method' / 'run' / 'end'
  / 'if' / 'then' / 'elseif' / 'else' / 'endif' / 'goto'
# / ...

STATEMENT <-
  labelDeclaration
/ 'goto' _ <LABELNAME> {
  }
/ ifStatementBlock

labelDeclaration <-
  <IDENT> _ ':' {
  }

ifStatementBlock <-
  'if' _ CONDITION _ 'then' _ STATEMENT {
  }
/ 'if' _ CONDITION
    (_ !'elseif' !'else' !'endif' !'then' STATEMENT)*
  (_ 'elseif' _ CONDITION
    (_ !'elseif' !'else' !'endif' !'then' STATEMENT)*
  )*
  (_ 'else'
    (_ !'elseif' !'else' !'endif' !'then' STATEMENT)*
  )?
  _ 'endif'

CONDITION <-
  IDENT _ '==' _ IDENT

BASICTYPE <- ('int' / 'uint' / 'char' / 'bool')

CONSTINIT <-
  '[' _ CONSTEXPR (_ ',' _ CONSTEXPR)* _ ']' {
    printf( "  = [array]\n" );
  }
/ CONSTEXPR

CONSTEXPR <-
  CONSTINT / IDENT

CONSTINT <-
  '0b'<[_0-1]+> {
    printf( "  = BININT: %s\n", $1 );
  }
/ '0o'<[_0-7]+> {
    printf( "  = OCTINT: %s\n", $2 );
  }
/ '0x'<[_0-9a-fA-F]+> {
    printf( "  = HEXINT: %s\n", $3 );
  }
/ <[_0-9]+> {
    printf( "  = INTEGER: %s\n", $4 );
  }

EXPR <-
  INTNUM

INTNUM <-
  '0b'<[_0-1]+> {
    printf( "  = BININT: %s\n", $1 );
  }
/ '0o'<[_0-7]+> {
    printf( "  = OCTINT: %s\n", $2 );
  }
/ '0x'<[_0-9a-fA-F]+> {
    printf( "  = HEXINT: %s\n", $3 );
  }
/ <[_0-9]+> {
    printf( "  = INTEGER: %s\n", $4 );
  }

LABELNAME <- IDENT

IDENT <- [_a-zA-Z][_a-zA-Z0-9]*

IDENTNAME <- !reservedWord IDENT

_ <- (space / eol / singleLineComment / multiLineComment)*

singleLineComment <-
  '//' (!eol !eof .)* (eol / eof)

# Rule adapted from:
# https://stackoverflow.com/questions/28419972/how-do-you-parse-nested-comments-in-pegjs
multiLineComment <-
  '/*' ((!'/*' !'*/' (eol / .)) / multiLineComment)* '*/'

space <- [ \t]

eol <- ('\r\n' / '\r' / '\n') {
  auxil->line++;
}

eof <- !.

%header {
  /// Begin parse state auxil declarations
  typedef struct SourceFile {
    FILE* handle;
  } SourceFile;

  typedef struct ParseState {
    SourceFile source;
    unsigned line;
    unsigned errLine;
  } ParseState;
  // End parse state auxil declarations
}

%auxil "ParseState*"

%source {
  // begin strnlen copied from packcc.c source file, as a workaround for TinyCC
  #ifdef __TINYC__
  static size_t strnlen(const char *str, size_t maxlen) {
    size_t i;
    for( i = 0; (i < maxlen) && str[i]; i++ ) {
    };
    return i;
  }
  #endif
  // end strnlen

/*
  static const char *dbg_str[] = { "Evaluating rule", "Matched rule", "Abandoning rule" };
  #define PCC_DEBUG(event, rule, level, pos, buffer, length) \
      fprintf(stderr, "%*s%s %s @%d [%.*s]\n", level * 2, "", dbg_str[event], rule, pos, length, buffer);
*/

  /// Begin PCC_GETCHAR override
  int ReadChar( ParseState* auxil ) {
    int result = EOF;
    if( auxil && auxil->source.handle ) {
      result = fgetc(auxil->source.handle);
    }
    if( result == '\n' ) {
      auxil->errLine++;
    }
    return result;
  }
  #define PCC_GETCHAR(auxil) ReadChar(auxil);
  // End PCC_GETCHAR override

  /// Begin PCC_ERROR override
  void SyntaxError( ParseState* auxil ) {
    printf( "Syntax error on line %u\n", auxil->errLine );
    exit( 1 );
  }
  #define PCC_ERROR(auxil) SyntaxError(auxil)
  // End PCC_ERROR override

}

%%

ParseState parseState = {NULL, 1, 1};

int main( int argc, char* argv[] ) {
  pcc_context_t *ctx = pcc_create(&parseState);

  parseState.source.handle = fopen(argv[1], "r");

  while( pcc_parse(ctx, NULL) ) {
  };

  pcc_destroy( ctx );

  fclose( parseState.source.handle );
  parseState.source.handle = NULL;

  return 0;
}
