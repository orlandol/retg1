
%header {
  /// Begin parse state auxil declarations
  typedef struct SourceFile {
    FILE* handle;

    unsigned line;
    unsigned column;

    unsigned nextLine;
    unsigned nextColumn;

    char ch;
    char nextch;
  } SourceFile;

  typedef struct ParseState {
    SourceFile source;
  } ParseState;
  // End parse state auxil declarations
}

%auxil "ParseState* "

%source {
  // begin strnlen copied from packcc.c source file, as a workaround for TinyCC
  #ifdef __TINYC__
  static size_t strnlen(const char *str, size_t maxlen) {
    size_t i;
    for( i = 0; (i < maxlen) && str[i]; i++ ) {
    };
    return i;
  }
  #endif
  // end strnlen

/*
  static const char *dbg_str[] = { "Evaluating rule", "Matched rule", "Abandoning rule" };
  #define PCC_DEBUG(event, rule, level, pos, buffer, length) \
      fprintf(stderr, "%*s%s %s @%d [%.*s]\n", level * 2, "", dbg_str[event], rule, pos, length, buffer);
*/

  /// Begin PCC_GETCHAR override
  int ReadChar( ParseState* auxil ) {
    if( auxil && auxil->source.handle ) {
      auxil->source.line = auxil->source.nextLine;
      auxil->source.column = auxil->source.nextColumn;

      auxil->source.ch = auxil->source.nextch;
      auxil->source.nextch = fgetc(auxil->source.handle);

      if( auxil->source.nextch != EOF ) {
        if( auxil->source.ch == 13 ) {
          auxil->source.ch = 10;
          if( auxil->source.nextch == 10 ) {
            auxil->source.nextch = fgetc(auxil->source.handle);
          }
        }

        if( auxil->source.ch == 10 ) {
          auxil->source.nextLine++;
          auxil->source.nextColumn = 0;
        }

        auxil->source.nextColumn++;
      }
    }
    return auxil->source.ch;
  }
  #define PCC_GETCHAR(auxil) ReadChar(auxil);
  // End PCC_GETCHAR override
}

### Begin grammar definitions

source <-
  _ declare_program _ declare_run _ eof {
    printf(
      "\n"
      "segment .data use32\n"
    );
  }

# program IDENT
declare_program <-
  'program' _ <ident> {
    printf( "; PROGRAM[%s]\n", $1 );
    printf(
      "\n"
      "BITS 32\n"
      "\n"
      "  extern ExitProcess\n"
      "  import ExitProcess kernel32.dll\n"
      "\n"
      "segment .code use32\n"
      "\n"
    );
  }

# top level declarations
#top_level_declarations <- _

# run block declarations
declare_run <-
  declare_run_begin
  _
  declare_run_end

declare_run_begin <-
  'run' {
    printf( "; RUN_BEGIN\n" );
    printf(
      "\n"
      "..start:\n"
      "  push    ebp\n"
      "  mov     ebp, esp\n"
      "\n"
    );
  }

declare_run_end <-
  'end' {
    printf( "; RUN_END\n" );
    printf(
      "  mov     esp, ebp\n"
      "  pop     ebp\n"
      "\n"
      "  push    dword 0\n"
      "  call    [ExitProcess]\n"
      "\n"
    );
  }

# qualified_ident rule adapted from:
# https://stackoverflow.com/questions/51397025/peg-parsing-declarations-with-arbitrary-number-of-qualifiers
#qualified_ident <-
#  (qualifier _ '.')* _ <ident> {
#    printf( "IDENT[%s]\n", $1 );
#  }
#
#qualifier <-
#  _ <ident> {
#    printf( "QUALIFIER[%s]\n", $1 );
#  }

ident <- [_a-zA-Z][_a-zA-Z0-9]*

_ <- (space / eol / single_line_comment / multi_line_comment)*

single_line_comment <- '//' (!eol !eof .)* (eol / eof)

# multi_line_comment rule adapted from:
# https://stackoverflow.com/questions/28419972/how-do-you-parse-nested-comments-in-pegjs
multi_line_comment <- '/*' ((!'/*' !'*/' .) / multi_line_comment)* '*/'

space <- [ \t]

eol <- '\r' / '\n' / '\r\n'

eof <- !.

### End grammar definitions

%%

ParseState parseState = {};

void Cleanup() {
  if( parseState.source.handle ) {
    fclose( parseState.source.handle );
    parseState.source.handle = NULL;
  }
}

int main( int argc, char* argv[] ) {
  atexit( Cleanup );

  pcc_context_t *ctx = pcc_create(&parseState);

  fprintf( stderr,
    "\n"
    "Retineo G1 Copyright 1993-2023 Orlando Llanes\n"
    "\n"
  );

  if( argc != 3 ) {
    fprintf( stderr, "Usage: retg1 source.ret binary.exe\n" );
    exit(1);
  }

  parseState.source.handle = fopen(argv[1], "rb");
  if( parseState.source.handle == NULL ) {
    printf( "Error opening source file '%s'\n", argv[1] );
    exit(2);
  }

  parseState.source.ch = EOF;
  parseState.source.nextch = EOF;
  ReadChar( &parseState );
  parseState.source.line = 1;
  parseState.source.column = 1;
  parseState.source.nextLine = 1;
  parseState.source.nextColumn = 1;

  while( pcc_parse(ctx, NULL) ) {
  }

  Cleanup();

  pcc_destroy( ctx );

  return 0;
}
